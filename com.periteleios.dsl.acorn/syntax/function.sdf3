module function

imports base
imports core

context-free sorts 
  ThisCall
  OptSort 
  Block 
  Function

context-free syntax

  //Exp.Fun = "function" "(" {ID ","}* ")" "{" Exp "}"
  
  Function.Function = [ func [FunId] [OptFormalArgs] [OptSort] [Block] ]
  
  OptSort.OptSortNone = []
  
  OptSort.OptSortSome = [: [Sort]]
  
  Definition.GlobalFunction = [ [Function] ]
  
  Exp.FunctionExp = [func [OptFormalArgs] [OptSort] [Block]]
  
  Exp.FunctionRef = [func . [FunId] ( [{Sort ","}*] ) [OptSort]]
  
  Exp.FunctionRefCall = [func . [FunId] ( [{Sort ","}*] ) [OptSort] ( [{Exp ","}*] )]
  
  Exp.FunctionRefCallPartial = [func . [FunId] ( [{Sort ","}*] ) [OptSort] ( * , [{Exp ","}*] )]
  
  Block.Block = [{
    [Exp*]
  }]
  
context-free syntax // function

  Exp = ThisCall
  
  ThisCall.ThisCall = [[FunId] ( [{Exp ","}*] )]
  
  Exp.Call = [[Exp].[Id] ( [{Exp ","}*] )]
    
  SimpleExp.SimpleThisCall = ThisCall
  
  SimpleExp.SimpleCall = [[SimpleExp] . [Id] ( [{Exp ","}*] )]

  
lexical syntax

 VarId = "func" {reject}
 
 
template options
  Id = keyword {reject} 
  keyword -/- [a-zA-Z0-9] 
 
